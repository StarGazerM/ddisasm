//===- code_inference.dl ------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This module computes the valid instructions organized in blocks of
code.  It infers the facts 'code_in_block', 'block', and 'code'. In
addition it computes 'block_still_overlap' for debugging purposes.

The disassembly is done in two traversals:

  - A "must" backward traversal that propagates invalid instructions
  - A "may" forward traversal that tries to generate a
    superset of all possible code blocks (with the predicates possible_target
    and code_in_block_candidate). This traversal has a recursive component
    and a linear-sweep component.

Finally, we detect candidate blocks of code that overlap with each other (and
therefore are in conflict) and give them points according to several
heuristics. Then for each two conflicting blocks we keep the one with
more points.
*/


// These are final addresses where the code is located
// They are organized in blocks of sequential code
// The block identifier is the address of the first instruction in the block
.decl code_in_block(ea:address,block:address)
.decl block(block:address)
.decl code(ea:address)
.decl block_last_instruction(block:address,EA:address)
.decl block_boundaries(block:address,BegAddr:address,EndAddr:address)
.decl overlapping_instruction(EA:address, EA2: address)
.decl data_in_code(Begin:address,End:address)
 // The postprocess refines the blocks by merging contiguous blocks
 // that are always executed together
#include "code_inference_postprocess.dl"

// cfg generates a control flow graph
#include "cfg.dl"

// we detect if there are still overlapping blocks,
// in that case the analysis has failed to resolve all conflicts
.decl block_still_overlap(ea:address,ea2:address)
.output block_still_overlap

/////////////////////////////////////////////////////////////
// Basic facts to define fallthrough

.decl may_fallthrough(o:address,d:address)

may_fallthrough(From,To):-
    next(From,To),
    !arch.return(From),
    !unconditional_jump(From),
    !halt(From),
    !no_return_call(From).

.decl must_fallthrough(o:address,d:address)

must_fallthrough(From,To):-
    may_fallthrough(From,To),
    instruction_get_operation(From,Operation),
    !arch.call_operation(Operation),
    !arch.syscall_operation(Operation),
    !arch.interrupt_operation(Operation),
    !instruction_has_loop_prefix(From),
    !conditional_jump(From).


//////////////////////////////////////////////////////////////
// This is a small refinement for discarding immediates as targets
// in some obvious cases. This is specially useful for PIE code where
// the address space starts from 0 (and therefore we have many more collisions).

.decl may_have_symbolic_immediate(src:address,dest:address)

may_have_symbolic_immediate(Src,as(Dest,address)):-
    instruction_immediate_offset(Src,Index,Offset),
    instruction_get_op(Src,Index,Op),
    op_immediate(Op,Dest),
    (
        binary_type("DYN"),
        relocation(Src+Offset,_,_,_)
        ;
        binary_type("REL"),
        relocation(Src+Offset,_,_,_)
        ;
        binary_type("EXEC"),
        instruction_get_operation(Src,Operation),
        !arch.logic_operation(Operation)
    ).


.decl invalid(EA:address)

// if the decoder failed to decode any instruction at EA, EA is invalid
invalid(EA):-
    invalid_op_code(EA).

// if EA contains a direct call or jump to something that cannot be code, it is invalid
invalid(EA):-
    (
        must_fallthrough(EA,Not_code);
        direct_jump(EA,Not_code);
        direct_call(EA,Not_code)
        //pc_relative_jump(EA,Not_code);
        //pc_relative_call(EA,Not_code)
    ),
    !instruction(Not_code,_,_,_,_,_,_,_,_,_).

// propagate the invalid instructions backwards until ret or jmp (encoded in must_fallthrough)
// propagate through direct jumps and calls
invalid(From):-
    invalid(To),
    (
        must_fallthrough(From,To);
        direct_jump(From,To);
        direct_call(From,To);
        pc_relative_jump(From,To);
        pc_relative_call(From,To)
    ).

.decl possible_ea(EA:address)

possible_ea(EA):-
    instruction(EA,_,_,_,_,_,_,_,_,_),
    !invalid(EA).

.decl basic_target(ea:address)

basic_target(Val):-
    (
        relative_address(_,_,_,_,Val,_)
        ;
        binary_type("DYN"),
        address_in_data(EA,Val),
        relocation(EA,_,_,_)
        ;
        binary_type("REL"),
        address_in_data(EA,Val),
        relocation(EA,_,_,_)
        ;
        binary_type("EXEC"),
        address_in_data(_,Val)
        ;
        entry_point(Val)
        ;
        code_section(Name),
        section(Name,_,Val)
        ;
        defined_symbol(Val,_,_,_,_,_)
        ;
        // Exception information
        fde_addresses(Val,_)
        ;
        lsda_callsite_addresses(Val, _, _)
        ;
        lsda_callsite_addresses(_, _, Val)
    ).

.decl block_limit(EA:address)

block_limit(as(EA,address)):-
    (
        op_immediate(_,EA);
        pc_relative_operand(_,_,EA);
        basic_target(EA);
        instruction_has_loop_prefix(EA);
        fde_addresses(_,EA);
        lsda_callsite_addresses(_,EA, _)
    ).

// The targets are computed incrementally now as we traverse the code
// likely_ea and possible_target_from are mutually recursive

.decl possible_target_from(dest:address,src:address)

possible_target_from(Dest,Src):-
    code_in_block_candidate(Src,_),
    (
        may_have_symbolic_immediate(Src,Dest)
        ;
        direct_jump(Src,Dest);
        direct_call(Src,Dest);
        pc_relative_jump(Src,Dest);
        pc_relative_call(Src,Dest);
        pc_relative_operand(Src,_,Dest)
    ).

.decl possible_target(Target:address)
.decl code_in_block_candidate(EA:address,EA_block:address)
.output code_in_block_candidate

possible_target(EA):-
    basic_target(EA).

possible_target(EA):-
    possible_target_from(EA,_).

// start a new block given a possible target
code_in_block_candidate(EA,EA):-
    possible_target(EA),
    possible_ea(EA).

// extend the block as long as we are sure to fallthrough and we have not
// reached a block limit
code_in_block_candidate(EA,Start):-
    code_in_block_candidate(EA2,Start),
    must_fallthrough(EA2,EA),
    !block_limit(EA).


// if reached a block limit or an instruction that does not necessarily
// fallthrough continue exploring but start a new block
code_in_block_candidate(EA,EA):-
    code_in_block_candidate(EA2,_),
    may_fallthrough(EA2,EA),
    (
        !must_fallthrough(EA2,EA);
        block_limit(EA)
    ),
    possible_ea(EA).


// in case we cannot fallthrough, we skip any nops that we might find
// and generate a new possible_target once the nops finish
.decl after_end(EA:address,End:address)

after_end(Next,End):-
    code_in_block_candidate(End,_),
    next(End,Next),
    !may_fallthrough(End,Next).

after_end(Next,End):-
    after_end(EA,End),
    is_padding(EA),
    !block_limit(EA),
    next(EA,Next).

after_end(Next,End):-
    relative_address(End,Size,_,_,_,_), Next=End+Size,
    !relative_address(Next,_,_,_,_,_),
    is_padding(Next).

possible_target(EA):-
    after_end(EA,_),
    (
        !arch.is_nop(EA);
        block_limit(EA)
    ).

///////////////////////////////////////////////////////////////////////
// We are done with the recursive exploration
// Now we detect and resolve conflics within the different blocks

//First remove common tails
.decl common_tail(EA:address)

common_tail(EA):-
    code_in_block_candidate(EA,Block),
    code_in_block_candidate(EA,Block2),
    Block != Block2,
    next(EA_prev,EA),
    code_in_block_candidate(EA_prev,Block),
    !code_in_block_candidate(EA_prev,Block2).

.decl code_in_block_candidate_refined(EA:address,Block:address)
.output code_in_block_candidate_refined

code_in_block_candidate_refined(Block,Block):-
    code_in_block_candidate(Block,Block).

code_in_block_candidate_refined(Block,Block):-
    common_tail(Block).

code_in_block_candidate_refined(EA_next,Block):-
    code_in_block_candidate_refined(EA,Block),
    next(EA,EA_next),
    // they used to be in the same block
    code_in_block_candidate(EA_next,OldBlock),
    code_in_block_candidate(EA,OldBlock),
    !code_in_block_candidate(EA_next,EA_next),
    !common_tail(EA_next).

.decl block_candidate_boundaries(BlockId:address,StartAddr:address,EndAddr:address)
.output block_candidate_boundaries

block_candidate_boundaries(Block,Block,Next):-
    !binary_isa("ARM"),
    code_in_block_candidate_refined(EA,Block),
    next(EA,Next),
    !code_in_block_candidate_refined(Next,Block).

block_candidate_boundaries(Block, BegAddr,EndAddr):-
    binary_isa("ARM"),
    code_in_block_candidate_refined(EA,Block),
    next(EA,Next),
    !code_in_block_candidate_refined(Next,Block),
    BegAddr = Block - (Block band 1),
    EndAddr = Next - (Next band 1).

// auxiliary predicate to detect that there are several candidates that fallthrough into a third candidate block
.decl multiple_fallthrough_to(Block:address)

multiple_fallthrough_to(Block):-
    may_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    may_fallthrough(EA2,Block),
    code_in_block_candidate_refined(EA2,BlockPred2),
    BlockPred != BlockPred2.

.decl instruction_range(EA:address,EA_Instruction:address)

instruction_range(EA2+1,EA2):-
    code_in_block_candidate_refined(EA2,_),
    next(EA2,End),
    EA2+1 < End.
instruction_range(EA+1,EA2):-
    instruction_range(EA,EA2),
    next(EA2,End),
    EA+1 < End.

.decl block_overlap(ea:address,ea2:address)
.output block_overlap


block_overlap(Block1,Block2):-
    block_candidate_boundaries(Block1,BegAddr1,EndAddr1),
    block_candidate_boundaries(Block2,BegAddr2,_),
    Block1 != Block2,
    BegAddr1 <= BegAddr2,
    BegAddr2 < EndAddr1.


.decl block_is_overlapping(block:address)

block_is_overlapping(Block):-
    (
        block_overlap(Block,_);
        block_overlap(_,Block)
    ).

//////////////////////////////////////////////////////////////////////
// We need to solve the block overlaps
// we do it with heuristics
///
.decl block_total_points(ea_block:address, points:number)

block_total_points(Block,Points):-
    block_is_overlapping(Block),
    Points = sum X:{block_points(Block,_,X,_)}.

.decl discarded_block(ea_block:address)
.output discarded_block

discarded_block(Block):-
    (
        block_overlap(Block,Block2);
        block_overlap(Block2,Block)
    ),
    // keep the block that has more points
    block_total_points(Block,Points),
    block_total_points(Block2,Points2),
    Points < Points2.

discarded_block(Block):-
    block_total_points(Block,Points),
    Points < 0.

// propagate discarding blocks through direct jumps or calls
discarded_block(Block):-
    discarded_block(Block2),
    (
        direct_jump(EA,Block2);
        direct_call(EA,Block2)
    ),
    code_in_block_candidate_refined(EA,Block).

//////////////////////////////////////////////////////////////////////
//special cases

// two blocks where the first instruction is overlapping and only different because of a prefix
// we discard the second block but register the instruction without prefix as code
overlapping_instruction(Block2,Block),
discarded_block(Block2):-
    block_overlap(Block,Block2),
    // same number of points
    block_total_points(Block,Points),
    block_total_points(Block2,Points),
    // one has an extra prefix with respect to the other
    instruction(Block,Size,_,OpCode,Op1,Op2,Op3,Op4,_,_),
    instruction(Block2,Size2,_,OpCode,Op1,Op2,Op3,Op4,_,_),
    Block2 - Block = Size - Size2.


// Auxiliary predicates to detect candidate blocks that are padding
.decl candidate_block_is_not_padding(Block:address)

candidate_block_is_not_padding(Block):-
    code_in_block_candidate_refined(EA,Block),
    !is_padding(EA).

.decl candidate_block_is_padding(Block:address)
.output candidate_block_is_padding

candidate_block_is_padding(Block):-
    code_in_block_candidate_refined(_,Block),
    !candidate_block_is_not_padding(Block).

/**
 The heuristics give a variable number of points for things like number
 of predecessors, how the block is reached (through direct jump, fallthough, etc.)
 and if the block's address appears in the data section, whether it appears aligned
 or in a pointer array.
 The points are trasmitted transitively though overlapping blocks.
*/


.decl block_points(block:address,predecessor:address,importance:number,why:symbol)
.output block_points

.decl transitive_points(type:symbol)

.decl overlap_with_jump_table(block:address)

transitive_points("start point").
transitive_points("code section start").
transitive_points("function symbol").
transitive_points("direct jump").
transitive_points("direct call").
transitive_points("must-fallthrough").
transitive_points("may-fallthrough").
transitive_points("fits").
transitive_points("fde-entry").
transitive_points("landing-pad").
transitive_points("exc-region").

block_is_overlapping(Block),
block_points(Block,0,-6,"overlap with relative-jump-table"):-
    block_candidate_boundaries(Block,BegBlockAddr,EndBlockAddr),
    relative_address(Beg,_,_,_,_,_),
    BegBlockAddr <= Beg, Beg< EndBlockAddr.


block_is_overlapping(Block),
block_points(Block,0,-6,"overlap with relative-jump-table"):-
    relative_address(Beg,Size,_,_,_,_),
    block_candidate_boundaries(Block,BegBlockAddr,_),
    Beg < BegBlockAddr, BegBlockAddr< Beg+Size.

overlap_with_jump_table(Block),
block_points(Block,0,-3,"possible relative-jump-table start"):-
    block_is_overlapping(Block),
    block_candidate_boundaries(Block,BegBlockAddr,EndBlockAddr),
    relative_address_start(Beg,_,_,_,_),
    BegBlockAddr <= Beg, Beg< EndBlockAddr.

block_points(Block,0,0,"basic point"):-
    block_is_overlapping(Block),
    code_in_block_candidate_refined(_,Block).

block_points(Block,0,20,"start point"):-
    block_is_overlapping(Block),
    entry_point(Block).

block_points(Block,0,1,"code section start"):-
    block_is_overlapping(Block),
    code_section(Section),
    section(Section,_,Block).

block_points(Block,0,20,"function symbol"):-
    block_is_overlapping(Block),
    function_symbol(Block,_).

block_points(Block,0,1,"address in data"):-
    block_is_overlapping(Block),
    address_in_data(Address,Block),
    Address % 8 != 0.

block_points(Block,0,2,"aligned address in data"):-
    block_is_overlapping(Block),
    address_in_data(Address,Block),
    Address % 8 = 0.

block_points(Block,0,-1,"printable address"):-
    block_is_overlapping(Block),
    address_in_data(Address,Block),
    data_byte(EA,Byte),(printable_char(Byte); Byte = 0),
    data_byte(EA+1,Byte1),(printable_char(Byte1); Byte1 = 0),
    data_byte(EA+2,Byte2),(printable_char(Byte2); Byte2 = 0),
    data_byte(EA+3,Byte3),(printable_char(Byte3); Byte3 = 0),
    data_byte(EA+4,Byte4),(printable_char(Byte4); Byte4 = 0),
    data_byte(EA+5,Byte5),(printable_char(Byte5); Byte5 = 0),
    data_byte(EA+6,Byte6),(printable_char(Byte6); Byte6 = 0),
    data_byte(EA+7,Byte7),(printable_char(Byte7); Byte7 = 0).

block_points(Block,0,1,"address in data array"):-
    block_is_overlapping(Block),
    address_in_data(Address,Block),
    Address % 8 = 0,
    (
        address_in_data(Address+8,_),
        address_in_data(Address+16,_)
        ;
        address_in_data(Address-8,_),
        address_in_data(Address-16,_)
        ;
        address_in_data(Address+8,_),
        address_in_data(Address-8,_)
    ).

block_points(Block,BlockPred,6,"direct jump"):-
    block_is_overlapping(Block),
    direct_jump(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred).

block_points(Block,BlockPred,6,"direct call"):-
    block_is_overlapping(Block),
    direct_call(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred).

block_points(Block,BlockPred,2,"must-fallthrough"):-
    block_is_overlapping(Block),
    must_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    !candidate_block_is_padding(BlockPred),
    !block_is_overlapping(BlockPred).

block_points(Block,BlockPred,7,"may-fallthrough"):-
    block_is_overlapping(Block),
    may_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    !candidate_block_is_padding(BlockPred),
    !block_is_overlapping(BlockPred).

// it fits but the previous block does not fallthrough
block_points(Block,BlockPred,3,"fits"):-
    block_is_overlapping(Block),
    after_end(Block,EA),
    code_in_block_candidate_refined(EA,BlockPred),
    !block_is_overlapping(BlockPred).

// fits after nops block
block_points(Block,BlockPred,1,"fits"):-
    block_is_overlapping(Block),
    must_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    candidate_block_is_padding(BlockPred),
    !block_is_overlapping(BlockPred).

// its address appears somewhere
block_points(Block,0,3,"appears"):-
    block_is_overlapping(Block),
    possible_target_from(Block,EA),
    !direct_jump(EA,Block),
    !direct_call(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred).

block_points(Block,0,3,"relative jump table target"):-
    block_is_overlapping(Block),
    relative_address(_,_,_,_,Block,_),
    !relative_address(Block,_,_,_,_,_).

// give a point if it may fallthrough to another block
block_points(Block,Block_succ,1,"fallthrough_to"):-
    block_is_overlapping(Block),
    code_in_block_candidate_refined(EA,Block),
    may_fallthrough(EA,Block_succ),
    code_in_block_candidate_refined(_,Block_succ),
    !block_is_overlapping(Block_succ).

// transitivity for fallthrough and direct jumps and calls

block_points(Block,BlockPred,Points,Why):-
    block_is_overlapping(Block),
    may_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    block_is_overlapping(BlockPred),
    !overlap_with_jump_table(BlockPred),
    block_points(BlockPred,_,Points,Why),
    transitive_points(Why),
    Points != 0.

block_points(Block,BlockPred,Points2,Why):-
    block_is_overlapping(Block),
    after_end(Block,EA),
    code_in_block_candidate_refined(EA,BlockPred),
    block_is_overlapping(BlockPred),
    !overlap_with_jump_table(BlockPred),
    block_points(BlockPred,_,Points,Why),
    transitive_points(Why),
    (
        Points > 2, Points2 = Points-2
        ;
        Points < -2, Points2 = Points+2
    ).

block_points(Block,BlockPred,Points,Why):-
    block_is_overlapping(Block),
    direct_jump(EA,Block),
    // do not count instructions that fallthrough and jump to the same place twice
    !may_fallthrough(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    block_is_overlapping(BlockPred),
    !overlap_with_jump_table(BlockPred),
    block_points(BlockPred,_,Points,Why),
    transitive_points(Why),
    Points != 0.

block_points(Block,BlockPred,Points,Why):-
    block_is_overlapping(Block),
    direct_call(EA,Block),
    code_in_block_candidate_refined(EA,BlockPred),
    block_is_overlapping(BlockPred),
    !overlap_with_jump_table(BlockPred),
    block_points(BlockPred,_,Points,Why),
    transitive_points(Why),
    Points != 0.

block_points(Block,0,1,"has well-defined jump"):-
    block_is_overlapping(Block),
    code_in_block_candidate_refined(EA,Block),
    direct_jump(EA,BlockDest),
    !block_is_overlapping(BlockDest).

block_points(Block,0,1,"has well-defined call"):-
    block_is_overlapping(Block),
    code_in_block_candidate_refined(EA,Block),
    direct_call(EA,BlockDest),
    !block_is_overlapping(BlockDest).

block_points(Block,0,1,"plt call"):-
    block_is_overlapping(Block),
    code_in_block_candidate_refined(EA,Block),
    direct_call(EA,BlockDest),
    plt_entry(BlockDest,_).

block_points(Block,0,10,"fde-entry"):-
    block_is_overlapping(Block),
    fde_addresses(Block,_).

block_points(Block,0,2,"padding nop block"):-
    block_is_overlapping(Block),
    fde_addresses(_,Block),
    arch.is_nop(Block).

block_points(Block,0,-1,"overlap long nop"):-
    block_is_overlapping(Block),
    block_overlap(Block,Block2),
    code_in_block_candidate_refined(EA_nop,Block2),
    arch.is_nop(EA_nop).

block_points(Block,0,3,"exc-region"):-
    block_is_overlapping(Block),
    lsda_callsite_addresses(Block, _, _).

block_points(Block,0,10,"landing-pad"):-
    block_is_overlapping(Block),
    lsda_callsite_addresses( _, _, Block).
////////////////////////////////////////////////////////////////////////////


code_in_block(EA,Block):-
    code_in_block_candidate_refined(EA,Block),
    !discarded_block(Block).

code_in_block(EA,Block):-
    overlapping_instruction(EA,Block),
    code_in_block(Block,Block).

block_last_instruction(Block,EA):-
    code_in_block(EA,Block),
    next(EA,Next),
    !code_in_block(Next,Block).

block_boundaries(Block,BegAddr,EndAddr),
block(Block):-
    code_in_block(_,Block),
    block_candidate_boundaries(Block,BegAddr,EndAddr).

code(EA):-
    code_in_block(EA,_).

block_still_overlap(Block,Block2):-
    block_overlap(Block,Block2),
    !discarded_block(Block),
    !discarded_block(Block2).


.decl data_in_code_propagate(Current:address,Initial:address,SectionLimit:address)

data_in_code_propagate(BlockEndAddr,BlockEndAddr,SectionLimit):-
    block_boundaries(_,BlockBegAddr,BlockEndAddr),
    !block_boundaries(_,BlockEndAddr,_),
    section(_,Size,AddrSection),
    SectionLimit = AddrSection+Size,
    AddrSection <= BlockBegAddr, BlockEndAddr < SectionLimit.

data_in_code_propagate(EA+1,Begin,SectionLimit):-
    data_in_code_propagate(EA,Begin,SectionLimit),
    !block_boundaries(_,EA+1,_),
    EA+1 < SectionLimit.

data_in_code(Begin,End+1):-
    data_in_code_propagate(End,Begin,_),
    !data_in_code_propagate(End+1,Begin,_).


.decl next_block_in_section(Block:address,NextBlock:address)
// Blocks are either contiguous or separated by data segments
// This assumes there are no remaining overlapping blocks.

next_block_in_section(Block,NextBlock):-
    block_boundaries(Block,_,NextBlockBeg),
    block_boundaries(NextBlock,NextBlockBeg,_).

next_block_in_section(Block,NextBlock):-
    block_boundaries(Block,_,BlockEndAddr),
    data_in_code(BlockEndAddr,NextBlockBeg),
    block_boundaries(NextBlock,NextBlockBeg,_).
